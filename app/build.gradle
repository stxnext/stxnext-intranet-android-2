apply plugin: 'com.android.application'
apply plugin: 'io.fabric'

repositories {
    maven { url 'https://maven.fabric.io/public' }
}

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.3"
    useLibrary 'org.apache.http.legacy' //only for facebook-stetho 

    defaultConfig {
        applicationId "com.stxnext.intranet2"
        minSdkVersion 16
        targetSdkVersion 23
        versionCode 23
        versionName "2.4.1"
    }

    packagingOptions {
        exclude '.readme'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
    }

    signingConfigs {
        debug {
            storeFile file("keystore/debug.keystore")
            storePassword "android"
            keyAlias "androiddebugkey"
            keyPassword "android"
        }
        //define gradle StxNextKeys.repo variable in your home directory ~/.gradle/gradle.properties
        release {
            if (project.hasProperty("StxNextKeys.repo")) {
                def projectPropsFile = file(project.property("StxNextKeys.repo") + File.separator + "Intranet2Android.properties")
                if (projectPropsFile.exists()) {
                    Properties props = new Properties()
                    props.load(new FileInputStream(projectPropsFile))

                    storeFile file(file(project.property("StxNextKeys.repo") + props['RELEASE_STORE_FILE']))
                    storePassword props['RELEASE_STORE_PASS']
                    keyAlias props['RELEASE_ALIAS']
                    keyPassword props['RELEASE_KEY_PASS']
                }
            } else {
                println "======================================================="
                println "[ERROR] - Please configure release-compilation environment - e.g. in ~/.signing  directory"
                println "======================================================="
            }
        }
    }

    //todo: resolve issue and change to true
    lintOptions {
        abortOnError false
    }

    buildTypes {

        debug {
            debuggable true
            minifyEnabled false
            signingConfig signingConfigs.debug
        }

        release {
            debuggable false
            minifyEnabled true
            zipAlignEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }

        releasePubKey {
            debuggable false
            minifyEnabled true
            zipAlignEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.debug
        }
    }

    productFlavors {

        fakeBackend {
            applicationId "com.stxnext.management.android"
        }

        htmlBackend {
            applicationId "com.stxnext.management.android"
        }

    }
}

dependencies {
    compile 'com.google.android.gms:play-services-maps:8.4.0'
    compile 'com.android.support:design:23.3.0'
    compile 'com.android.support:appcompat-v7:23.3.0'
    compile 'com.android.support:cardview-v7:23.3.0'
    compile 'com.android.support:recyclerview-v7:23.3.0'
    compile 'com.android.support:percent:23.3.0'
    compile 'com.github.lzyzsd:circleprogress:1.1.0'
    compile 'com.squareup.picasso:picasso:2.5.2'
    compile 'com.squareup.okhttp:okhttp:2.4.0'
    compile 'com.google.code.gson:gson:2.4'
    compile 'com.google.guava:guava:18.0'
    compile 'joda-time:joda-time:2.3'
    compile 'com.j256.ormlite:ormlite-android:4.48'
    compile 'com.google.code.findbugs:jsr305:2.0.2'
    compile 'com.squareup.retrofit:retrofit:1.9.0'

    debugCompile 'com.facebook.stetho:stetho:1.1.1'
    debugCompile 'com.facebook.stetho:stetho-okhttp:1.1.1'
    debugCompile 'com.facebook.stetho:stetho-urlconnection:1.1.1'

    compile 'io.reactivex:rxandroid:1.1.0'
    compile 'com.jakewharton.rxbinding:rxbinding:0.4.0'
    compile 'com.jakewharton:butterknife:7.0.1'

    compile 'com.pixplicity.easyprefs:library:1.7'

    compile('com.crashlytics.sdk.android:crashlytics:2.3.2@aar') {
        transitive = true;
    }
}
